repositories {
    // Other repositories described by default:
    // CleanroomMC: https://maven.cleanroommc.com
    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots/'
    }
    maven {
        url 'https://jitpack.io'
    }
    exclusiveContent {
        forRepository {
            maven {
                name 'CurseMaven'
                url 'https://cursemaven.com'
            }
        }
        filter {
            includeGroup 'curse.maven'
        }
    }
    mavenLocal() // Must be last for caching to work
}

configurations {
    asyncProfiler
    implementation.extendsFrom(asyncProfiler)
}

dependencies {
    embed 'com.google.protobuf:protobuf-javalite:3.21.6'
    embed 'com.neovisionaries:nv-websocket-client:2.14'
    embed 'me.lucko:bytesocks-java-client-api:1.0-SNAPSHOT'

    asyncProfiler 'tools.profiler:async-profiler:4.0'

    implementation 'com.cleanroommc:configanytime:2.0'
}

jar {
    from(provider{ configurations.asyncProfiler.collect {it.isDirectory() ? it : zipTree(it)} }) {
        eachFile {
            if (it.path.endsWith('.so')) {
                it.path = "async-profiler/${it.path}"
            }
        }
    }
    includeEmptyDirs = false
}
